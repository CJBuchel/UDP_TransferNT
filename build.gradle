plugins {
	// Apply the cpp-library plugin to add support for building C++ libraries
	id 'cpp-library'

	// Apply the cpp-unit-test plugin to add support for building and running C++ test executables
	id 'cpp-unit-test'

	// Publish library to maven
	id 'maven-publish'

	// Add basic gradle conventions like archiving
	id 'base'
}

apply from: 'config.gradle'
apply from: 'vendor.gradle'

allprojects {

	group = artifactGroupId
	version = artifactVersion

	publishing {
		repositories {
			maven {
				if (localMaven) {
					println "Using Local Maven"
					name = "wml_udp_transfernt_maven"
					url = "${buildDir}/maven"
				} else {
					println "Using Remote maven - I sure hope you know what your doing"
					println System.getenv("WML_USERNAME")
					println System.getenv("WML_TOKEN")
					url = mavenUrl
					credentials {
						username = project.findProperty("gpr.user") ?: System.getenv("WML_USERNAME")
						password = project.findProperty("gpr.key") ?: System.getenv("WML_TOKEN")
					}
				}
			}

			publications.all {
				groupId project.group
			}
		}
	}
}

library {
	linkage = [Linkage.SHARED, Linkage.STATIC]

	targetMachines = [
		machines.linux.x86_64
		// machines.windows.x86, machines.windows.x86_64,
		// machines.macOS.x86_64
	]

	source {
		def cppTree = project.fileTree('lib/src/main/cpp')
		cppTree.include "**/*.c"
		cppTree.include "**/*.cc"
		cppTree.include "**/*.cpp"

		from cppTree
	}

	publicHeaders {
		from file('lib/src/main/public')
	}

	baseName = "UDP_TransferNT"
}

unitTest {

	targetMachines = [
		machines.linux.x86_64
		// machines.windows.x86, machines.windows.x86_64,
		// machines.macOS.x86_64
	]

	source {
		def cppTree = project.fileTree('lib/src/test/cpp')
		cppTree.include "**/*.c"
		cppTree.include "**/*.cc"
		cppTree.include "**/*.cpp"

		from cppTree
	}
}

tasks.withType(LinkExecutable).configureEach {
	linkerArgs.add '-pthread'
}

// Library publishing
apply from: "publish.gradle"